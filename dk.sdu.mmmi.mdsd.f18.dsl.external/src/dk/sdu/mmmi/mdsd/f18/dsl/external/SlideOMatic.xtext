grammar dk.sdu.mmmi.mdsd.f18.dsl.external.SlideOMatic with org.eclipse.xtext.common.Terminals

generate slideOMatic "http://www.sdu.dk/mmmi/mdsd/f18/dsl/external/SlideOMatic"

// Project Extension is '.slide'
Presentation:
	"presentation" name=STRING (subtitle=STRING)? "{" ("(" (theme=Theme)? (authors=Authors)? (institute=Institute)?
	(date=Date)? ")")?
	(slides+=Slide |
	animations+=Animation)* "}";

Theme:
	"theme" theme=BeamerTheme ("color" themeColor=ThemeColor)?;

BeamerTheme:
	"AnnArbor" | "Antibes" | "Bergen" | "Berkeley" | "Berlin" | "Boadilla" | "boxes" | "CambridgeUS" | "Copenhagen" |
	"Darmstadt" | "default" | "Dresden" | "Frankfurt" | "Goettingen" | "Hannover" | "Ilmanau" | "JuanLesPins" | "Luebeck"
	| "Madrid" | "Malmoe" | "Marburg" | "Montpellier" | "PaloAlto" | "Pittsburgh" | "Rochester" | "Singapore" | "Szeged"
	| "Warsaw";

ThemeColor:
	"albatros" | "beaver" | "beetle" | "crane" | "default" | "dolphin" | "dove" | "fly" | "lily" | "orchid" | "rose" |
	"seagull" | "seahorse" | "sidebartab" | "structure" | "whale" | "wolverine";

Authors:
	("author" | "authors") names+=STRING (',' names+=STRING)*;

Institute:
	"institute" name=STRING;

Date:
	"date" (date=STRING | {CompileDate} "*");

Slide:
	"slide" (sec=Section name=STRING | name=STRING)? "{" contents+=Content+ "}";

Section:
	{Sec} "*" | {SubSec} "**" | {SubSubSec} "***";

Content:
	(ToC | Text | Block | List | Image | Table | Code) (click=Click)?;

ToC:
	{ToC} "ToC" | {CurrentSecToC} "ToC" "*";

Text:
	text=STRING;

Block:
	"block" (name=STRING)? "{" content+=Content+ "}";

List:
	NumberedList | UnNumberedList;

NumberedList:
	"#list" items+=ListItem+;

UnNumberedList:
	"&list" items+=ListItem+;

ListItem:
	"-" item=STRING ("(" nestedList=List ")")? (click=Click)?;

Image:
	"img" name=ID "src" src=STRING (alignment=Alignment)? (size=Size);

Alignment:
	"topleft" | "top" | "topright" | "left" | "center" | "right" | "bottomleft" | "bottom" | "bottomright";

Size:
	{ProportionalSize} way=Way scale=INT "%" | {ExactSize} size=INT unit=("cm" | "em" | "mm");

Way:
	{Width} "width" | {Height} "height";

Table:
	"tab" name=ID "[" rows+=TableRow ("," rows+=TableRow)* "]";

TableRow:
	"[" values+=STRING ("," values+=STRING)* "]";

Code:
	"```" lang=ID code=STRING "```";

Click:
	"$$";

Animation:
	"animate" target=[Image] type=AnimationType "to" location=Alignment (size=Size)?;

AnimationType:
	{Move} "move" | {Jump} "jump";
	